import com.android.build.gradle.internal.LoggerWrapper
import com.android.build.gradle.internal.tasks.DeviceProviderInstrumentTestTask
import com.android.build.gradle.internal.tasks.databinding.DataBindingMergeDependencyArtifactsTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import io.gitlab.arturbosch.detekt.Detekt

plugins {
    id("com.starter.config")
    id("com.starter.versioning")
    id("com.osacky.doctor") version("0.7.3")
    id("com.starter.easylauncher") version("5.0.1") apply(false)
    id "com.github.ben-manes.versions" version "0.42.0"
}

commonConfig {
    javaFilesAllowed false
    javaVersion JavaVersion.VERSION_11
}

allprojects {
    repositories {
        exclusiveContent {
            forRepository {
                repositories.maven {
                    setUrl("https://oss.sonatype.org/content/repositories/snapshots/")
                    mavenContent { snapshotsOnly() }
                }
            }
            filter {
                includeModule("app.cash.paparazzi", "paparazzi")
                includeModule("app.cash.paparazzi", "paparazzi-agent")
            }
        }
    }
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs = freeCompilerArgs +
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi" +
                "-opt-in=kotlin.RequiresOptIn" +
                "-opt-in=kotlin.time.ExperimentalTime" +
                "-opt-in=kotlinx.coroutines.FlowPreview"
        }
    }
    ["com.android.library", "com.android.application"].forEach {
        pluginManager.withPlugin(it) {
            tasks.withType(DataBindingMergeDependencyArtifactsTask).configureEach {
                outputs.cacheIf { false }
            }
            android {
                composeOptions {
                    kotlinCompilerExtensionVersion libs.versions.google.compose.core.get()
                }
                packagingOptions {
                    exclude 'META-INF/*.kotlin_module'
                }
            }
        }

        tasks.withType(DeviceProviderInstrumentTestTask).configureEach {
            doFirst {
                LoggerWrapper.Switch.shouldShowInfoLogsAsLifecycle = true
            }
            doLast {
                LoggerWrapper.Switch.shouldShowInfoLogsAsLifecycle = false
            }
        }
    }
    pluginManager.withPlugin("com.android.library") {
        android {
            namespace "io.github.wykopmobilny${path.replace(":", ".").replace("-", "_")}"
            variantFilter {
                ignore = buildType.name != "debug"
            }
        }
    }
    pluginManager.withPlugin("com.android.application") {
        android.buildTypes.configureEach {
            setMatchingFallbacks("debug")
        }
    }
    pluginManager.withPlugin("com.starter.quality") {
        tasks.withType(Detekt).configureEach {
            baseline.set(file("$rootDir/detekt-baseline.xml"))
            buildUponDefaultConfig = true
            config.setFrom(rootProject.file("detekt-config.yml"))
        }
    }
    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == "org.checkerframework" && requested.name != "checker-compat-qual") {
                useVersion(libs.versions.mavencentral.checker.get())
                because("https://github.com/google/ExoPlayer/issues/10007")
            }
        }
    }
}
